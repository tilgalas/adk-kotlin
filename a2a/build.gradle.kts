/*
 * Copyright 2026 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  kotlin("multiplatform")
  alias(libs.plugins.vanniktech.maven.publish)
}

kotlin {
  jvm()

  sourceSets {
    val commonMain by getting {
      dependencies {
        implementation(project(":google-adk-kotlin-core"))
        implementation(libs.kotlinx.coroutines.core)
      }
    }
    val commonTest by getting { dependencies { implementation(kotlin("test")) } }
    val commonJvmAndroidMain by creating {
      dependsOn(commonMain)
      dependencies {
        implementation(libs.jackson.databind)
        implementation(libs.jackson.datatype.jsr310)
        implementation(libs.jackson.module.kotlin)
        implementation(libs.a2a.sdk.client)
        implementation(libs.a2a.sdk.common)
        implementation(libs.a2a.sdk.spec)
        implementation(libs.a2a.sdk.transport.rest)
      }
    }
    val jvmMain by getting { dependsOn(commonJvmAndroidMain) }
  }
}

// See comment in `core/build.gradle.kts` for how the vanniktech plugin
// derives per-target artifact IDs for multiplatform modules and why we
// override the javadoc jar source to Dokka.
mavenPublishing {
  configure(
    com.vanniktech.maven.publish.KotlinMultiplatform(
      javadocJar = com.vanniktech.maven.publish.JavadocJar.Dokka("dokkaHtml")
    )
  )
  coordinates(artifactId = "google-adk-kotlin-a2a")
}
